// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx
    And16(a=x, b=false, out=andZeroX);
    Mux16(a=x, b=andZeroX, sel=zx, out=ozx);

    // nx
    Not16(in=ozx, out=notZX);
    Mux16(a=ozx, b=notZX, sel=nx, out=onx);

    // zy
    And16(a=y, b=false, out=andZeroY);
    Mux16(a=y, b=andZeroY, sel=zy, out=ozy);

    // ny
    Not16(in=ozy, out=notZY);
    Mux16(a=ozy, b=notZY, sel=ny, out=ony);

    // f
    And16(a=onx, b=ony, out=andxy);
    Add16(a=onx, b=ony, out=addxy);
    Mux16(a=andxy, b=addxy, sel=f, out=of);

    // no
    Not16(in=of, out=nof);
    Mux16(a=of, b=nof, sel=no, out=out, out[0..7]=lor, out[8..15]=ror, out[15]=ong);

    // zr
    Or8Way(in=lor, out=llor);
    Or8Way(in=ror, out=rror);
    Or(a=llor, b=rror, out=nzr);
    Not(in=nzr, out=zr);

    // ng
    Or(a=ong, b=false, out=ng);
}